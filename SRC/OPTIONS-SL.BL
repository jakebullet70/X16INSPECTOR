## BASLOAD ISO CRLF 
#CONTROLCODES 1

REM --- THIS CODE IS SHARED BETWEEN THE MAIN AND OPTIONS PROGRAM
REM --- THIS CODE IS SHARED BETWEEN THE MAIN AND OPTIONS PROGRAM
REM --- THIS CODE IS SHARED BETWEEN THE MAIN AND OPTIONS PROGRAM

REM --- THIS MAIN GAME CODE CALLS THE 'OPTIONS.INIT.VARS'  (FILE WILL BE CREATED IF MISSING)
REM --- THEN CALLS 'OPTIONS.LOAD'

REM --- DIM SUB.MENU.OPTIONS.NDX	 : REM --- WHERE IN THE ARRAY TO LOOK
REM --- DIM OPTIONS.DEFAULT.RET.VAL	 : REM --- RET VAL FROM THE FIND ME SUB ROUTINE

REM --- 
REM --- DIM OPTIONS.SOUND : REM --- 1=ON / 2=OFF 
REM --- DIM OPTIONS.DIFICULTY : REM --- 1=EASY / 2=HARD
REM --- DIM OPTIONS.THEME  : REM --- 1=DARK / 2 = LIGHT
REM --- DIM OPTIONS.KB.JOY : REM --- 1=KB / 2=JOY
REM --- 

OPTIONS.INIT.VARS:
	REM --- CALL THIS 1ST
	FILE.NAME$ = "INSPECTOR.DAT"
	GOSUB OPTIONS.FILE.EXISTS
	GOSUB OPTIONS.LOAD
	RETURN


OPTIONS.FILE.EXISTS:
	GOSUB FILE.EXISTS
	IF TMP THEN RETURN
	GOSUB OPTIONS.INIT.FILE
	RETURN

	
OPTIONS.INIT.FILE:	
	REM --- CREATE A DEFUALT OPTIONS FILE
	FILE.NAME.TMP$ = FILE.NAME$ + ",S,W" 
	OPEN 2,8,4,FILE.NAME.TMP$
	PRINT#2, "MODE=1" 	: PRINT#2, "SOUND=1" 
	PRINT#2, "THEME=1" 	: PRINT#2, "KB/JOY=1"
	CLOSE 2
	RETURN
	
	
OPTIONS.LOAD:
	FILE.NAME.TMP$ = FILE.NAME$ + ",S,R" 
	OPEN 2,8,2,FILE.NAME.TMP$
	REM --- READ THE 4 OPTIONS (TODO SET THE GLBAL VARS)
	INPUT#2,TMP$ : OPTIONS.DIFICULTY	= VAL(RIGHT$(TMP$,1)) 
	INPUT#2,TMP$ : OPTIONS.SOUND		= VAL(RIGHT$(TMP$,1))
	INPUT#2,TMP$ : OPTIONS.THEME		= VAL(RIGHT$(TMP$,1))
	INPUT#2,TMP$ : OPTIONS.KB.JOY  		= VAL(RIGHT$(TMP$,1))
	CLOSE 2
	RETURN
	
		
OPTIONS.SAVE:
	DOS "S:" + FILE.NAME$ : REM --- DELETE THE OLD FILE
	
	FILE.NAME.TMP$ = FILE.NAME$ + ",S,W" 
	OPEN 2,8,2,FILE.NAME.TMP$
	
	SUB.MENU.OPTIONS.NDX = 1 : GOSUB OPTIONS.FIND.DEFAULT.VAL : REM --- DIFICULTY
	PRINT#2, "MODE=" + STR$(OPTIONS.DEFAULT.RET.VAL) 
	
	SUB.MENU.OPTIONS.NDX = 2 : GOSUB OPTIONS.FIND.DEFAULT.VAL : REM --- SOUND
	PRINT#2, "SOUND=" + STR$(OPTIONS.DEFAULT.RET.VAL)
	
	SUB.MENU.OPTIONS.NDX = 3 : GOSUB OPTIONS.FIND.DEFAULT.VAL : REM --- THEME
	PRINT#2, "THEME=" + STR$(OPTIONS.DEFAULT.RET.VAL)
	
	SUB.MENU.OPTIONS.NDX = 4 : GOSUB OPTIONS.FIND.DEFAULT.VAL : REM --- KB / JOY
	PRINT#2, "KB/JOY=" + STR$(OPTIONS.DEFAULT.RET.VAL)
	
	CLOSE 2
	RETURN
	
	
	
OPTIONS.FIND.DEFAULT.VAL:
	REM --- SCROLL THROUGH THE SUB-MENU ARRAY LOOKING FOR THE DEFAULT
	FOR TMP2 = 1 TO MENUS.SUB.ITEMS.TTL(SUB.MENU.OPTIONS.NDX)
		IF MNU.DEFAULT(SUB.MENU.OPTIONS.NDX,TMP2) = 1 THEN OPTIONS.DEFAULT.RET.VAL = TMP2 : RETURN
	NEXT TMP2
	OPTIONS.DEFAULT.RET.VAL = 0
	RETURN


FILE.EXISTS:
	REM --- CAN THIS BE DOME WITH THE NEW DOS COMMAND EASIER?
	TMP = 0
	FILE.NAME.TMP$ = FILE.NAME$ + ",S,R" 
	OPEN 2,8,2,FILE.NAME.TMP$ : CLOSE 2
	OPEN 15,8,15 
	INPUT#15,TMP0,TMP$,TMP1,TMP2 : CLOSE 15 
	TMP = (TMP0 <= 20)
	RETURN

   
   
   