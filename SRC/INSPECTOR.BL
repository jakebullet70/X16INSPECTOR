## ------------------------------------------------------
## BASLOAD ISO CRLF 
##
##
##
##
##
## ------------------------------------------------------
#CONTROLCODES 1
#SYMFILE "@:INSPECTOR.SYM"
#SAVEAS "@:INSPECTOR.PRG"

DIM MENU.TEXT$(10) : DIM MENU.TYPE(10) : DIM MENU.ATTR(4) 	: REM -- USED BY MENUS-PROMPT.BL

DIM OPTIONS.SOUND : REM --- 1=ON / 2=OFF 
DIM OPTIONS.DIFICULTY : REM --- 1=EASY / 2=HARD
DIM OPTIONS.THEME  : REM --- 1=DARK / 2 = LIGHT
DIM OPTIONS.KB.JOY : REM --- 1=KB / 2=JOY --------------- !!!!!!!!!!! NOT USED ANYMORE !!!!!!!!!!!!!!!!


GOSUB SAVE.ENV
## GOSUB SYSTEM.FONT.SET.NOT.ISO.MODE : GOSUB SYSTEM.KEYBOARD.SET.PETSCIIGRAPHICS
POKE $030C,4 : SYS $FF62 : REM --- CHANGE TO THIN PET UPPER/GRAPHICS
GOSUB SOUND.INIT : GOSUB OPTIONS.INIT.VARS : SCREEN 3

IF OPTIONS.THEME = 1 THEN GOSUB SET.THEME.DARK
IF OPTIONS.THEME = 2 THEN GOSUB SET.THEME.LIGHT


DEBUG = 1

## AUTO NOTE TAKING - SAVE TXT FROM EACH SUSPECT TO ARRAY, SHOW IN EDIT PRG WHEN FKEY
## AUTO NOTE TAKING - SAVE TXT FROM EACH SUSPECT TO ARRAY, SHOW IN EDIT PRG WHEN FKEY
## AUTO NOTE TAKING - SAVE TXT FROM EACH SUSPECT TO ARRAY, SHOW IN EDIT PRG WHEN FKEY
## AUTO NOTE TAKING - SAVE TXT FROM EACH SUSPECT TO ARRAY, SHOW IN EDIT PRG WHEN FKEY


## --- TRUE GLOBALS VARS --------------------------
DIM SOLVED.CASE.PARTS(3) 
DIM OZ(5,9) : DIM SUSPECTS$(5) : DIM ROOMS$(6)
SUSPECTS$(1)="JOHN":SUSPECTS$(2)="STACEY":SUSPECTS$(3)="BILL": SUSPECTS$(4)="ANDY":SUSPECTS$(5)="SUZY"
ROOMS$(1)="GARAGE":ROOMS$(2)="CLOSET":ROOMS$(3)="OFFICE": ROOMS$(4)="KITCHEN":ROOMS$(5)="LIBRARY":ROOMS$(6)="BASEMENT"
INTRO.MNU.DIMMED = 0 : SELECTED.SUSPECT.NDX = 1
## ------------------------------------------------

START:

	COLOR VAL(CLR.FGTEXT.NORMAL$),VAL(CLR.BACKGROUND$) 
	GOSUB DRAW.INTRO.SCREEN
	CLS : GOSUB DRAW.DIAGRAM
	
	FOR X = 1 TO 5
		OZ(X,1) = INT(6 * RND(1)) + 1
	NEXT X

	FOR X = 2 TO 9
		FOR Y = 1 TO 5
			CONTINUE.FOR.450: 
			A = INT(6 * RND(1)) + 1
			IF A = OZ(Y,X - 1) THEN GOTO CONTINUE.FOR.450
			OZ(Y,X) = A
		NEXT Y
	NEXT X

	MURDERER.NAME.NDX = INT(5 * RND(1)) + 1
	MURDERER.TIME     = INT(9 * RND(1)) + 1
	MURDERER.ROOM.NDX = OZ(MURDERER.NAME.NDX,MURDERER.TIME)
	
	GOSUB LOAD.ALL.SPRITES
	
	IN.SUSPECT.PICKER.SCRN = 0
	
	IF MURDERER.NAME.NDX = 1 THEN GOTO LINE580
	
	FOR X = 1 TO MURDERER.NAME.NDX - 1
		CONTINUE.FOR.99:
		IF OZ(X,MURDERER.TIME) = MURDERER.ROOM.NDX THEN OZ(X,MURDERER.TIME) = (INT(5*RND(1)) + 1): GOTO CONTINUE.FOR.99
	NEXT X
	IF MURDERER.NAME.NDX = 5 THEN GOTO START.QUESTIONS

LINE580:
	FOR X = MURDERER.NAME.NDX + 1 TO 5
		CONTINUE.FOR.590:
		IF OZ(X,MURDERER.TIME) = MURDERER.ROOM.NDX THEN OZ(X,MURDERER.TIME) = (INT(5 * RND(1)) + 1): GOTO CONTINUE.FOR.590
	NEXT X
	 
  
START.QUESTIONS:

	## --- GET A SUSPECT TO QUESTION
	GOSUB CHANGE_MENU_SOUND
	## IF SELECTED.SUSPECT.NDX = 0 THEN SELECTED.SUSPECT.NDX = 1
	CURRENT.SPRITE.NUM = SELECTED.SUSPECT.NDX
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	
	IF DEBUG = 1 THEN GOSUB DEBUG.SHOW
	
	LOCATE 16,3 
	
	PRINT "QUESTION SOMEONE OR SOLVE THE CASE?";
	
	GOSUB MENU.SUSPECTS.INIT : GOSUB MENU.SUSPECTS.RUN
	
	IF MENU.NDX = 6 THEN GOTO SOLVE.CASE.NOW
	CURENT.SUSPECT$ = SUSPECTS$(MENU.NDX) 
	SELECTED.SUSPECT.NDX = MENU.NDX 
		
 
QUESTION.TIME.OR.PLACE:
	## --- ASK TIME OR PLACE
	
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	LOCATE 16,3 
	PRINT RIGHT$(CLR.FGTEXT.NORMAL$,LEN(CLR.FGTEXT.NORMAL$)-2) ; "WHAT DO YOU WANT TO ASK " CURENT.SUSPECT$ "?"; 
	GOSUB MENU.QUESTONS.TYPE.INIT : GOSUB MENU.QUESTONS.TYPE.RUN
	REM --- IF MENU.NDX = 3 THEN GOTO START.QUESTIONS
	ON MENU.NDX GOTO QUESTION.TIME, QUESTIONS.ROOM, START.QUESTIONS
  

## =========================================================================
## =========================================================================
## =========================================================================


QUESTION.TIME:

	GOSUB CHANGE_MENU_SOUND
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	LOCATE 16,3 : PRINT SUSPECTS$(SELECTED.SUSPECT.NDX);
	LOCATE 17,3 : PRINT "WHERE WERE YOU AT {RVS ON}  1  {RVS OFF}'O CLOCK?"; 
	KB.JOY.PROMPT$ = "LEFT/RIGHT CHANGE TIME. FIRE TO CONTINUE" : GOSUB SHOW.KB.JOY.PROMPT
	## PRINT CURENT.SUSPECT$"...  WHERE WERE THEY AT (TIME) ";: INPUT ZT: PRINT : A1 = A1 + 1
		
	TIME.OF.MURDER = 1
	
	## --- START OF LOOP
	KB.SCAN.START:
		GOSUB KB.JOY.KEYS.GET
		SLEEP(10)
		IF KB.STR$ = "R" OR KB.STR$ = "U" THEN TIME.OF.MURDER = TIME.OF.MURDER + 1 : GOSUB QUESTION_TIME_VALID_TIME : GOTO KB.SCAN.START
		IF KB.STR$ = "L" OR KB.STR$ = "D" THEN TIME.OF.MURDER = TIME.OF.MURDER - 1 : GOSUB QUESTION_TIME_VALID_TIME : GOTO KB.SCAN.START
		IF KB.STR$ = "F" THEN GOTO KB.SCAN.END : 	REM --- EXIT LOOP
		GOTO KB.SCAN.START : REM --- DO THE LOOP AGAIN
	KB.SCAN.END:
	
	IF SELECTED.SUSPECT.NDX = MURDERER.NAME.NDX THEN GOTO LINE870 : REM--- THIS IS THE MURDERER! SO HE MIGHT LIE TO YOU!
	GOTO LINE890
	
QUESTION_TIME_VALID_TIME:	
	## - TODO, MAYBE A 'BINK' SOUND WHEN CHAGING THE TIMES?
	IF TIME.OF.MURDER < 1 THEN TIME.OF.MURDER = 1  
	IF TIME.OF.MURDER > 9 THEN TIME.OF.MURDER = 9
	LOCATE 17,22 : PRINT RIGHT$(CLR.FGTEXT.NORMAL$,LEN(CLR.FGTEXT.NORMAL$)-2) ; "{RVS ON}"  TIME.OF.MURDER  "{RVS OFF}"; 
	RETURN

LINE870:
	IF RND(1) < .5 THEN GOTO LINE890
	LU = INT(5 * RND(1)) + 1 : GOTO QUESTION.TIME.ANSWER
	
LINE890:
	LU = OZ(SELECTED.SUSPECT.NDX,TIME.OF.MURDER)
		
		
QUESTION.TIME.ANSWER:
	GOSUB CHANGE_MENU_SOUND
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	## GOSUB CLEAR.PROMPT.SCREEN : GOSUB SHOW.MUGSHOT : GOSUB KB.BUFFER.CLEAR
	LOCATE 17,3 
	PRINT CURENT.SUSPECT$ ", AT" TIME.OF.MURDER "'CLOCK"
	LOCATE 19,1
	PRINT "I WAS IN THE " ROOMS$(LU)
	
	REM --- THIS MIGHT NEED MORE WORK, IT WAS THE DIFFERANCE BETWEEN THE +4 VERSION AND THE C64 VERSION
	REM --- THE C64 VERSION WAS CLAIMED TO BE EASIER, IT HAD THE .3 AND THE +4 HAD THE .2
	IF OPTIONS.DIFICULTY = 2 AND RND(1) > .2 THEN GOTO LINE940 : REM --- HARD
	IF OPTIONS.DIFICULTY = 1 AND RND(1) > .3 THEN GOTO LINE940 : REM --- EASY
		
	IF TIME.OF.MURDER > MURDERER.TIME THEN PRINT "BY THEN MR KIM WAS ALREADY DEAD!" : GOTO LINE940 
	PRINT "BY THEN MR KIM WAS STILL ALIVE!"	
	

LINE940:
	REM -- 940 
	REM --- WITHOUT THE USE OF BUGLINE VAR WE ARE GETTING A LF BETWEEN PRINT STATMENTS
	BUGLINE = 22 
	FOR X = 1 TO 5
		IF X = SELECTED.SUSPECT.NDX THEN GOTO LINE970
		IF OZ(X,TIME.OF.MURDER) = LU THEN GOSUB TIME.SHOW.INFO.ALSO
		## ------ IF OZ(X,TIME.OF.MURDER) = LU THEN PRINT ""SUSPECTS$(X) " WAS ALSO THERE"
		LINE970:
	NEXT X

	REM -- 980 
	REM --- WITHOUT THE USE OF BUGLINE VAR WE ARE GETTING A LF BETWEEN PRINT STATMENTS
	FOR X = 1 TO 5
		IF SELECTED.SUSPECT.NDX = X THEN GOTO LINE1010
		IF (OZ(X,TIME.OF.MURDER) = LU-1) OR (OZ(X,TIME.OF.MURDER) = LU + 1) THEN GOSUB TIME.SHOW.INFO.ALSO
		## ----- IF (OZ(X,TIME.OF.MURDER) = LU-1) OR (OZ(X,TIME.OF.MURDER) = LU + 1) THEN PRINT ""SUSPECTS$(X)" WAS ALSO THERE" 
		LINE1010:
	NEXT X

	## SLEEP 120
	GOSUB SHOW.CONTINUE.TXT.STR	
	GOSUB KB.JOY.KEYS.GET : REM --- JUST WAIT FOR ANY KEY

LINE1020:
	GOTO START.QUESTIONS	
	
TIME.SHOW.INFO.ALSO:
	REM --- WITHOUT THE USE OF BUGLINE VAR WE ARE GETTING A LF BETWEEN PRINT STATMENTS
	LOCATE BUGLINE,1
	PRINT SUSPECTS$(X)" WAS ALSO THERE" 
	BUGLINE=BUGLINE+1
	RETURN

		

## =========================================================================
	
QUESTIONS.ROOM:
	GOSUB CHANGE_MENU_SOUND
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	LOCATE 17,1 :PRINT CURENT.SUSPECT$ ", WHEN WERE YOU IN THE..."
	GOSUB MENU.QUESTONS.ROOMS.INIT : GOSUB MENU.QUESTONS.ROOMS.RUN
	SELECTED.ROOM.NDX = MENU.NDX
	
LINE1110:
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	LOCATE 18,1
	PRINT CURENT.SUSPECT$" SAYS:"
	 
	FOR X = 1 TO 5
		IF SUSPECTS$(X) = CURENT.SUSPECT$ THEN GOTO LINE1150
	NEXT X
		
LINE1150:
	SELECTED.SUSPECT.NDX = X 
	IF SELECTED.SUSPECT.NDX = MURDERER.NAME.NDX GOTO LINE1180 : REM -- 1160 
	GOTO LINE1260 : REM -- 1170 

LINE1180:
	IF MURDERER.ROOM.NDX = SELECTED.ROOM.NDX THEN GOTO LINE1200
	GOTO LINE1230

LINE1200:
	X = INT(RND(1) * 9 ) + 1
	GOSUB ROOM.SHOW.WHEN.TIME
	GOTO END.ROOM.QUESTION : REM GOTO LINE1020

LINE1230:
	IF RND(1)<.5 THEN GOTO LINE1260
	X = INT(RND(1) * 9) + 1
	IF RND(1)<.5 THEN GOSUB ROOM.SHOW.WHEN.TIME : GOTO END.ROOM.QUESTION : REM --GOTO LINE1020

LINE1260:
	X1=0  
	FOR X = 1 TO 9
		IF OZ(SELECTED.SUSPECT.NDX,X) = SELECTED.ROOM.NDX THEN GOSUB ROOM.SHOW.WHEN.TIME : X1=1
	NEXT X
	IF X1 = 0 THEN GOTO LINE1310
	GOTO END.ROOM.QUESTION : REM --GOTO LINE1020
 
LINE1310:
	PRINT"I WAS NEVER IN THE "ROOMS$(SELECTED.ROOM.NDX)
	## GOTO LINE1020
	
END.ROOM.QUESTION:
	## SLEEP 120
	GOSUB SHOW.CONTINUE.TXT.STR
	GOSUB KB.JOY.KEYS.GET : REM --- JUST WAIT FOR ANY KEY
	GOTO LINE1020

ROOM.SHOW.WHEN.TIME:
	PRINT "AT"X"{LEFT}'CLOCK I WAS IN THE "ROOMS$(SELECTED.ROOM.NDX)
	RETURN
 
 ## =========================================================================
 ## =========================================================================
 
UPDATE.SOLVED.GUESS:
	PRINT RIGHT$(CLR.FGTEXT.NORMAL$,LEN(CLR.FGTEXT.NORMAL$)-2);
	LOCATE 27,3 : PRINT "YOUR SUSPECT IS {RVS ON}"SUSPECTS$(GUESS.PICKED.SUSPECT)"{RVS OFF}  ";
	LOCATE 28,3 : PRINT "IN THE {RVS ON}"ROOMS$(GUESS.PICKED.ROOM)"{RVS OFF} AT {RVS ON}"RIGHT$(STR$(GUESS.PICKED.TIME),1)"'CLOCK{RVS OFF}   ";	
	RETURN
	
UPDATE.SOLVED.GUESS.CHANGE.SUSPECT:
	IF MENU.SCROLL.LR.SELECTED = 1 THEN GUESS.PICKED.SUSPECT = GUESS.PICKED.SUSPECT + 1 
	IF MENU.SCROLL.LR.SELECTED = 2 THEN GUESS.PICKED.SUSPECT = GUESS.PICKED.SUSPECT - 1
	IF GUESS.PICKED.SUSPECT > 5 THEN GUESS.PICKED.SUSPECT = 1 
	IF GUESS.PICKED.SUSPECT < 1 THEN GUESS.PICKED.SUSPECT = 5
	GOSUB SHOW.MUGSHOT
	RETURN
	
UPDATE.SOLVED.GUESS.CHANGE.ROOM:
	IF MENU.SCROLL.LR.SELECTED = 1 THEN GUESS.PICKED.ROOM = GUESS.PICKED.ROOM + 1 
	IF MENU.SCROLL.LR.SELECTED = 2 THEN GUESS.PICKED.ROOM = GUESS.PICKED.ROOM - 1
	IF GUESS.PICKED.ROOM > 6 THEN GUESS.PICKED.ROOM = 1 
	IF GUESS.PICKED.ROOM < 1 THEN GUESS.PICKED.ROOM = 6
	RETURN
	
UPDATE.SOLVED.GUESS.CHANGE.TIME:
	IF MENU.SCROLL.LR.SELECTED = 1 THEN GUESS.PICKED.TIME = GUESS.PICKED.TIME + 1 
	IF MENU.SCROLL.LR.SELECTED = 2 THEN GUESS.PICKED.TIME = GUESS.PICKED.TIME - 1
	IF GUESS.PICKED.TIME > 9 THEN GUESS.PICKED.TIME = 1 
	IF GUESS.PICKED.TIME < 1 THEN GUESS.PICKED.TIME = 9
	RETURN
 
SOLVE.CASE.NOW:
	GOSUB CHANGE_MENU_SOUND
	SHOW.THE.MUGSHOT = 1 : GOSUB SETUP_PROMPT_SCREEN
	GUESS.PICKED.SUSPECT = 1 : GUESS.PICKED.ROOM = 1 : GUESS.PICKED.TIME = 1 
	PRINT RIGHT$(CLR.FGTEXT.NORMAL$,LEN(CLR.FGTEXT.NORMAL$)-2);
	LOCATE 17,3 : PRINT "INSPECTOR, ARE YOU SAYING"
	LOCATE 18,3 : PRINT "YOU SOLVED THIS CASE?"
			
	## --- START OF MENU LOOP
	GOSUB MENU.SOLVED.INIT
	GOSUB UPDATE.SOLVED.GUESS
	
	SOLVED.SCAN.START:
		GOSUB MENU.SOLVED.RUN
		IF MENU.NDX = 1 AND MENU.SCROLL.LR.SELECTED > 0 THEN GOSUB UPDATE.SOLVED.GUESS.CHANGE.SUSPECT
		IF MENU.NDX = 2 AND MENU.SCROLL.LR.SELECTED > 0 THEN GOSUB UPDATE.SOLVED.GUESS.CHANGE.TIME
		IF MENU.NDX = 3 AND MENU.SCROLL.LR.SELECTED > 0 THEN GOSUB UPDATE.SOLVED.GUESS.CHANGE.ROOM
		IF MENU.NDX = 4 THEN GOTO CHECK.FOR.SOLVED.CASE
		IF MENU.NDX = 5 THEN GOTO SHOW.SOLVED.CASE.FAILURE
		IF MENU.NDX = 6 THEN GOTO START.QUESTIONS : REM --- EXIT MENU, BACK TO START
		GOSUB UPDATE.SOLVED.GUESS
	GOTO SOLVED.SCAN.START : REM --- SOLVED.SCAN.END
		
	
CHECK.FOR.SOLVED.CASE:

	SLEEP(20) : GOSUB KB.BUFFER.CLEAR

	GOSUB CHANGE_MENU_SOUND
	SHOW.THE.MUGSHOT = 0 : GOSUB SETUP_PROMPT_SCREEN
	
	LOCATE 17,3
	CASE.SOLVED = 0 : REM --- IF THIS = 3 THEN CASE IS SOLVED
	
	IF GUESS.PICKED.SUSPECT = MURDERER.NAME.NDX THEN  PRINT "INDEED! "SUSPECTS$(GUESS.PICKED.SUSPECT)" IS THE MURDERER!" : SOLVED.CASE.PARTS(1) = 1
	IF GUESS.PICKED.SUSPECT <> MURDERER.NAME.NDX THEN PRINT "NOPE,"SUSPECTS$(GUESS.PICKED.SUSPECT)" IS NOT THE MURDERER!"
	
	LOCATE 19,3
	
	IF GUESS.PICKED.TIME = MURDERER.TIME  THEN PRINT "FANTASTIC! THE MURDER TIME IS RIGHT!" : SOLVED.CASE.PARTS(2) = 1
	IF GUESS.PICKED.TIME <> MURDERER.TIME THEN PRINT "NO, YOU FAILED TO GET THE TIME RIGHT."
	
	LOCATE 21,3
	
	IF GUESS.PICKED.ROOM = MURDERER.ROOM.NDX THEN PRINT "CORRECT, THE " ROOMS$(MURDERER.ROOM.NDX) " IS THE LOCATION!": SOLVED.CASE.PARTS(3) = 1
	IF GUESS.PICKED.ROOM <> MURDERER.ROOM.NDX THEN PRINT "THE ROOM IS INCORECT."
	REM --  ARE YOU REALLY AN INSPECTOR?
	SLEEP 180
	
	COLOR VAL(CLR.FGTEXT.ALERT$) : LOCATE 29,4 
	PRINT "INCOMING TEXT FROM THE BIG BOSS";
	GOSUB KB.BUFFER.CLEAR : GOSUB KB.JOY.KEYS.GET : GOSUB CHANGE_MENU_SOUND
	
	IF (SOLVED.CASE.PARTS(3) = 1 AND SOLVED.CASE.PARTS(2) = 1) AND SOLVED.CASE.PARTS(1) = 1 THEN GOTO SHOW.SOLVED.CASE.SUCCESS
	GOTO SHOW.SOLVED.CASE.FAILURE
	
	
SHOW.SOLVED.CASE.SUCCESS:
	## --- WHAT A GUY!!!  TODO --- ADD SUCCESS SOUND - IF IS SOUND ON?
	GOSUB SHOW.SOLVED.CASE.HEADER
	PRINT "{DOWN}{DOWN}WHAT A GUY! SUCCESS!{DOWN}{DOWN}"
	PRINT "THE MURDERER " SUSPECTS$(MURDERER.NAME.NDX)" HAS JUST BEEN"
	##     0123456789012345678901234567890123456789
	PRINT "ARRESTED. YOU BUILT A SOLID CASE WITH"
	PRINT "ENOUGH EVIDENCE FOR AN ASSURED"
	PRINT "CONVICTION. A LETTER OF COMENDATION"
	PRINT "WILL BE ADDED TO YOUR PERSONAL FILE."
	PRINT "{DOWN}{DOWN}{DOWN}AGAIN... WHAT A GUY!
	GOSUB SHOW.CONTINUE.TXT.STR
	SLEEP(20) : GOSUB KB.BUFFER.CLEAR
	GOSUB KB.JOY.KEYS.GET
	GOTO START

SHOW.SOLVED.CASE.FAILURE:	
	## --- WHAT REALLY HAPPENED!!!    TODO --- ADD FAILURE SOUND - IF IS SOUND ON?
	GOSUB SHOW.SOLVED.CASE.HEADER
	##     0123456789012345678901234567890123456789
	PRINT "{DOWN}{DOWN}"
	PRINT "I'M DISAPPOINTED IN YOU!"
	PRINT "YOU REALLY ARE CLUE-LESS!"
	PRINT "{DOWN}{DOWN}HERE'S WHAT HAPPENED:"
	PRINT "{DOWN}AT AROUND "MURDERER.TIME"'CLOCK, " SUSPECTS$(MURDERER.NAME.NDX)" MURDERED"
	PRINT "MR KIM IN THE "ROOMS$(MURDERER.ROOM.NDX)"{DOWN}"
	PRINT "MAYBE DIRECTING TRAFIC WILL BE YOUR"
	PRINT "NEXT ASSIGNMENT? JUST CLUE-LESS..."
	GOSUB SHOW.CONTINUE.TXT.STR
	SLEEP(20) : GOSUB KB.BUFFER.CLEAR
	GOSUB KB.JOY.KEYS.GET
	GOTO START
	## CLOSE.APP
	
SHOW.SOLVED.CASE.HEADER:	
	GOSUB TURN.OFF.ALL.SPRITES
	PRINT RIGHT$(CLR.FGTEXT.NORMAL$,LEN(CLR.FGTEXT.NORMAL$)-2);
	CLS : PRINT "{DOWN}{DOWN}{DOWN}"
	##     0123456789012345678901234567890123456789
	PRINT "FROM: THE CHIEF INSPECTOR"
	PRINT "{DOWN}TO: INSPECTOR CLUE-LESS"
	PRINT RPT$(ASC("-"),38)
	RETURN

SHOW.CONTINUE.TXT.STR:
	KB.JOY.PROMPT$ = "          PRESS FIRE TO CONTINUE" 
	GOSUB SHOW.KB.JOY.PROMPT
	RETURN

CHANGE_MENU_SOUND:
	IF OPTIONS.SOUND <> 1 THEN RETURN
	GOSUB SOUND.CLICK : RETURN
	
DEBUG.SHOW:
	## ---  DEBUG 
	LOCATE 1,1 
	## MURDERER.NAME.NDX = MU
	## MURDERER.ROOM.NDX = RA
	## MURDERER.TIME = TZ
	## PRINT  "MEM:" ; FRE(0)
	PRINT  "MURDER:" SUSPECTS$(MURDERER.NAME.NDX) " TIME:"MURDERER.TIME" ROOM:"ROOMS$(MURDERER.ROOM.NDX);
	RETURN
 
 
REM --- INCLUDE FILES ---
#INCLUDE "/SRC/ENV-SAVE-LOAD.BL" 
#INCLUDE "/SRC/SOUNDS.BL"
#INCLUDE "/SRC/DRAWING-PETSCII.BL"
#INCLUDE "/SRC/MENUS-PROMPT.BL"
#INCLUDE "/SRC/KEYS-MOUSE.BL"
#INCLUDE "/SRC/OPTIONS-SL.BL"	
#INCLUDE "/SRC/INTRO.BL"	
#INCLUDE "/SRC/SYSTEM.BL"	
