## BASLOAD ISO CRLF 
#CONTROLCODES 1



	## --->  THIS CODE ENDS UP IN THE MAIN FILE
	## ---------------------------------------------------------------------
	## DIM MENU.TEXT$(10) 		: REM MENU BAR ITEMS 
	## DIM MENU.TYPE(10) 		: REM 1 = ALSO LEFT/RIGHT, 0 = NOPE
	## DIM MENU.ATTR(4) 		: REM 1=X,2=Y, 3=MENU BAR LEN,4 = TTL MENU ITEMS 
	
	## COLORS CONTAIN BOTH THE # AND THE PETSCII CODE
	## CLR.MNU.FG.BAR$     = "01{WHITE}"
	## CLR.MNU.BG.BAR$     = "00{BLACK}"
	## CLR.MNU.FG.HL.BAR$  = "02{RED}"
	## CLR.MNU.BG.HL.BAR$  = "06{GRN}"
	## ---------------------------------------------------------------------
	

## ------------------------------------------------------				
## -------  QUESTIONS ABOUT  ROOMS  ---------------------				
## ------------------------------------------------------		
		
		
MENU.QUESTONS.ROOMS.INIT:
	MENU.KB.TMP.RET.VAL = 0	
	GOSUB MENU.CLEAR.VARS
	
	MENU.TEXT$(1) = ROOMS$(1) : MENU.TEXT$(2) = ROOMS$(2) : MENU.TEXT$(3) = ROOMS$(3)
	MENU.TEXT$(4) = ROOMS$(4) : MENU.TEXT$(5) = ROOMS$(5) : MENU.TEXT$(6) = ROOMS$(6)
					
	MENU.ATTR(1) = 9										: REM X LOCATION
	MENU.ATTR(2) = 21										: REM Y LOCATION
	MENU.ATTR(3) = 15										: REM MAX BAR LEN
	MENU.ATTR(4) = 6										: REM TTL MENU ITEMS
			
	GOSUB MENU.CLEAR.SCRN 
	MENU.SCROLL.LR.SELECTED = 0 : MENU.NDX = 1
	GOSUB MENU.QUESTONS.ROOMS.RUN	
	RETURN 
	
MENU.QUESTONS.ROOMS.RUN:	
	GOSUB MENU.GO
	
	## MENU.NDX 				---> HAS THE SELECTED VALUE
	## MENU.SCROLL.LR.SELECTED 	---> HAS THE LEFT / RIGHT SELECTION  - 1 OR 2 - 0 = NOT SELECTED
	
	IF MENU.SELECTED > 0 OR MENU.SCROLL.LR.SELECTED > 0 THEN RETURN : REM -- MENU SELECTED - OR SIDE MENU
	
	GOTO MENU.QUESTONS.ROOMS.RUN
	
		

		


		
## ------------------------------------------------------				
## -------  QUESTIONS FOR SUSPECTS  ---------------------				
## ------------------------------------------------------		
		
		
MENU.QUESTONS.TYPE.INIT:
	MENU.KB.TMP.RET.VAL = 0	
	GOSUB MENU.CLEAR.VARS
	
	MENU.TEXT$(1) = "WHERE WERE YOU AT A CERTAIN TIME?"
	MENU.TEXT$(2) = "WHEN WERE YOU AT A CERTAIN PLACE?"  
	MENU.TEXT$(3) = "ASK ANOTHER SUSPECT"
		
	MENU.ATTR(1) = 4										: REM X LOCATION
	MENU.ATTR(2) = 21										: REM Y LOCATION
	MENU.ATTR(3) = LEN(MENU.TEXT$(1)) 						: REM MAX BAR LEN
	MENU.ATTR(4) = 3										: REM TTL MENU ITEMS
			
	GOSUB MENU.CLEAR.SCRN 
	MENU.SCROLL.LR.SELECTED = 0 : MENU.NDX = 1
	GOSUB MENU.QUESTONS.TYPE.RUN	
	RETURN 
	
MENU.QUESTONS.TYPE.RUN:	
	GOSUB MENU.GO
	
	## MENU.NDX 				---> HAS THE SELECTED VALUE
	## MENU.SCROLL.LR.SELECTED 	---> HAS THE LEFT / RIGHT SELECTION  - 1 OR 2 - 0 = NOT SELECTED
	
	IF MENU.SELECTED > 0 OR MENU.SCROLL.LR.SELECTED > 0 THEN RETURN : REM -- MENU SELECTED - OR SIDE MENU
	
	GOTO MENU.QUESTONS.TYPE.RUN
	
		
		
		
## ------------------------------------------------------				
## -------   SUSPECTS MENU ------------------------------				
## ------------------------------------------------------		
	
MENU.SUSPECTS.INIT:
	MENU.KB.TMP.RET.VAL = 0	
	GOSUB MENU.CLEAR.VARS
	
	MENU.TEXT$(1) = SUSPECTS$(1) 
	MENU.TEXT$(2) = SUSPECTS$(2)  	: MENU.TYPE(2) = 0
	MENU.TEXT$(3) = SUSPECTS$(3)  	: MENU.TYPE(3) = 0
	MENU.TEXT$(4) = SUSPECTS$(4)  	: MENU.TYPE(4) = 0
	MENU.TEXT$(5) = SUSPECTS$(5) 
	MENU.TEXT$(6) = "I SOLVED THE CASE!"
		
	MENU.ATTR(1) = 8										: REM X LOCATION
	MENU.ATTR(2) = 20										: REM Y LOCATION
	MENU.ATTR(3) = LEN(MENU.TEXT$(6)) 						: REM MAX BAR LEN
	MENU.ATTR(4) = 6										: REM TTL MENU ITEMS
		
		
	GOSUB MENU.CLEAR.SCRN 
	MENU.SCROLL.LR.SELECTED = 0 : MENU.NDX = 1
	GOSUB MENU.SUSPECTS.RUN	: RETURN 
	
MENU.SUSPECTS.RUN:	
	GOSUB MENU.GO
	
	## MENU.NDX 				---> HAS THE SELECTED VALUE
	## MENU.SCROLL.LR.SELECTED 	---> HAS THE LEFT / RIGHT SELECTION  - 1 OR 2 - 0 = NOT SELECTED
	
	IF MENU.SELECTED > 0 OR MENU.SCROLL.LR.SELECTED > 0 THEN RETURN : REM -- MENU SELECTED - OR SIDE MENU
	
	GOTO MENU.SUSPECTS.RUN
	
	

## ------------------------------------------------------
## ------------------------------------------------------
## ------------------------------------------------------
## ------------------------------------------------------

MENU.GO: 
	MENU.SCROLL.LR.SELECTED = 0	
	MENU.SELECTED = 0
	
MENU.LOOP1:
	FOR X = 1 TO MENU.ATTR(4)
		LOCATE X + MENU.ATTR(2)-1,MENU.ATTR(1)
		IF MENU.NDX <> X THEN COLOR VAL(CLR.MNU.FG.BAR$), VAL(CLR.MNU.BG.BAR$)
		IF MENU.NDX = X THEN COLOR VAL(CLR.MNU.FG.HL.BAR$), VAL(CLR.MNU.BG.HL.BAR$)
		PRINT MENU.TEXT$(X) ;
	NEXT
	
MENU.GET.KEYS:
	## VJ=JOY(0) -- NEED TO ADD JOYSTICK - TODO
	## GET TMPKB$ : MENU.KB.TMP.RET.VAL = ASC(TMPKB$)
	GOSUB KB.JOY.KEYS.GET : MENU.KB.TMP.RET.VAL = KBJOY.ASC
	SLEEP(4)
	IF MENU.KB.TMP.RET.VAL = 145 THEN MENU.NDX = MENU.NDX - 1 : GOTO MENU.PROCESS.KEY.UD
	IF MENU.KB.TMP.RET.VAL =  17  THEN MENU.NDX = MENU.NDX + 1 : GOTO MENU.PROCESS.KEY.UD
	IF (MENU.KB.TMP.RET.VAL = 29)  AND MENU.TYPE(MENU.NDX) = 1 THEN MENU.SCROLL.LR.SELECTED = 1 : RETURN
	IF (MENU.KB.TMP.RET.VAL = 157) AND MENU.TYPE(MENU.NDX) = 1 THEN MENU.SCROLL.LR.SELECTED = 2 : RETURN
	IF MENU.KB.TMP.RET.VAL =  13 THEN MENU.SELECTED = 1 : RETURN
	## JOYSTICK
	## IF VJ AND 4 THEN MENU.NDX = MENU.NDX + 1 : GOTO MENU.PROCESS.KEY.UD
	## IF VJ AND 8 THEN MENU.NDX = MENU.NDX - 1 : GOTO MENU.PROCESS.KEY.UD
	## IF (VJ AND 2) AND MENU.TYPE(MENU.NDX) = 1 THEN MENU.SCROLL.LR.SELECTED = 1 : RETURN
	## IF (VJ AND 1) AND MENU.TYPE(MENU.NDX) = 1 THEN MENU.SCROLL.LR.SELECTED = 2 : RETURN
	## IF VJ AND 16 THEN MENU.SELECTED = 1 : RETURN 
	GOTO MENU.GET.KEYS

MENU.PROCESS.KEY.UD:
	IF MENU.NDX = 0 THEN MENU.NDX = MENU.ATTR(4) 
	IF MENU.NDX = MENU.ATTR(4)+1 THEN MENU.NDX = 1 
	GOSUB MENU.CLEAR.KB
	GOTO MENU.LOOP1
	
MENU.CLEAR.KB:	
	GET TMPKB$ : IF LEN(TMPKB$) <> 0 GOTO MENU.CLEAR.KB	
	RETURN

MENU.CLEAR.VARS:
	FOR X23 = 1 TO 10 
		MENU.TEXT$(X23) = "" 
		MENU.TYPE(X23)  = 0 
	NEXT X23
	FOR X23 = 1 TO 4  : MENU.ATTR(X23) = 0 : NEXT X23
	RETURN

MENU.CLEAR.SCRN:
	COLOR VAL(CLR.MNU.FG.BAR$),VAL(CLR.MNU.BG.BAR$)
	FOR X23 = 1 TO MENU.ATTR(4)
		LOCATE X23 + MENU.ATTR(2) - 1,MENU.ATTR(1)
		PRINT RPT$(ASC(" "),MENU.ATTR(3))
	NEXT X23
	RETURN



## --------------------- THESE ARE GENERIC AND NEED TO BE #DEFINED!
## --------------------- THESE ARE GENERIC AND NEED TO BE #DEFINED!
## --------------------- THESE ARE GENERIC AND NEED TO BE #DEFINED!


KB.BUFFER.CLEAR:
	## --- CLEAR THE KEYBOARD
	GET KB.TMP$ : KBJOY.ASC = ASC(KB.TMP$)
	IF KBJOY.ASC <> 0 THEN GOTO KB.BUFFER.CLEAR
	RETURN

KB.JOY.KEYS.GET:
	## --- VARS - KBJOY.ASC, KB.STR$, KB.ATTR
	## --- VJ=JOY(0) - NEED TO ADD JOYSTICK - TODO
	GET KB.STR$ : KBJOY.ASC = ASC(KB.STR$)
	IF KBJOY.ASC <> 0 THEN RETURN
	GOTO KB.JOY.KEYS.GET
	

